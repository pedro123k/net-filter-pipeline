cmake_minimum_required(VERSION 3.20)

project(net-filter-pipeline VERSION 1.0 LANGUAGES CXX)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
include(FetchContent)

add_library(dsp 
    src/BiquadFilter.cpp
    src/DigitalFilter.cpp
    src/SignalPipeline.cpp
)

add_library(Boost_headers INTERFACE)
add_library(Boost::headers ALIAS Boost_headers)

add_library(udp_interface 
    src/UDPInterface.cpp
)

if (WIN32)
    find_path(BOOST_INCLUDE_DIR
    NAMES boost/asio.hpp
    PATH_SUFFIXES include
    PATHS
        "$ENV{MSYS2_PREFIX}"
        /ucrt64
        C:/msys64/ucrt64
    REQUIRED
    )
    target_include_directories(Boost_headers INTERFACE "${BOOST_INCLUDE_DIR}")
    target_link_libraries(Boost_headers INTERFACE ws2_32)
else ()
    find_package(Boost REQUIRED)
    if (TARGET Boost::boost)
        target_link_libraries(Boost_headers INTERFACE Boost::boost)
    else()
        target_include_directories(Boost_headers INTERFACE ${Boost_INCLUDE_DIRS})
    endif()
endif()

add_library(config_parse 
    src/ConfigsParse.cpp
)

FetchContent_Declare(
    nlohmann_json
    GIT_REPOSITORY https://github.com/nlohmann/json.git
    GIT_TAG v3.11.3
)

FetchContent_MakeAvailable(nlohmann_json)

target_link_libraries(udp_interface PUBLIC Boost::headers)

target_link_libraries(config_parse PUBLIC dsp)
target_link_libraries(config_parse PUBLIC udp_interface)
target_link_libraries(config_parse PUBLIC nlohmann_json)


target_include_directories(dsp PUBLIC 
    ${CMAKE_CURRENT_SOURCE_DIR}/include 
)

target_include_directories(udp_interface PUBLIC 
    ${CMAKE_CURRENT_SOURCE_DIR}/include 
)

target_compile_features(dsp PUBLIC cxx_std_17)

add_executable(app src/main.cpp)
target_link_libraries(app PRIVATE dsp)
target_link_libraries(app PRIVATE udp_interface)
target_link_libraries(app PRIVATE config_parse)

add_executable(test1_dsp tests/test1.cpp)
target_link_libraries(test1_dsp PRIVATE dsp)

add_executable(test2_dsp tests/test2.cpp)
target_link_libraries(test2_dsp PRIVATE dsp)

add_executable(test3_udp tests/test3.cpp)
target_link_libraries(test3_udp PRIVATE dsp)
target_link_libraries(test3_udp PRIVATE udp_interface)

add_executable(test4_confs tests/test4.cpp)
target_link_libraries(test4_confs PRIVATE config_parse)

